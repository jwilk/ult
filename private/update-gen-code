#!/usr/bin/env perl

# Copyright Â© 2024 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

no lib '.';  # CVE-2016-1238

use strict;
use warnings;
use v5.14;

use English qw(-no_match_vars);
use File::stat qw(stat);
use FindBin ();
use autodie;

my $here = $FindBin::Bin;
my $base = "$here/..";

sub munch
{
    my ($in, $out) = @_;
    my $gindent = undef;
    while (<$in>) {
        m/^( *)/ or die;
        my $indent = length $1;
        if (m{# generated by (private/\S+)}) {
            my $cmd = $1;
            print;
            open my $pipe, '-|', "$base/$cmd";
            while (<$pipe>) {
                print;
            }
            close $pipe;
            $gindent = $indent;
            next;
        }
        if (defined $gindent) {
            if ($indent > $gindent) {
                next;
            } else {
                $gindent = undef;
            }
        }
        print;
    }
    return;
}

my $path = "$base/ult";
open my $in, '<', $path;
open my $out, '>', "$path.tmp";
select $out;  ## no critic (ProhibitOneArgSelect)
munch($in, $out);
close $in;
close $out;
my $mode = (stat "$path")->mode;
chmod $mode, "$path.tmp";
rename "$path.tmp", $path;

# vim:ts=4 sts=4 sw=4 et
